// Pair example + client code.

// In this version we just abstract the location variables.
// The 'typestate' version of this example abstracts the capability
// type, thus creating a proper typestate.

import add

let newPair = fun( _ : [] ).
	open <pl,l> = new {} in
	open <pr,r> = new {} in
		// packs 'pl' as "left" and 'pr' as "right"
		<pl:left,<pr:right,
			{
			initL = fun( i : int :: rw pl [] ).( l := i ),
			initR = fun( i : int :: rw pr [] ).( r := i ),
			sum = fun( _ : [] :: ( rw pl int * rw pr int ) ).(add !l !r),
			destroy = fun ( _ : [] :: ( rw pl int * rw pr int ) ).( delete l; delete r ) 
			}::( rw pr [] * rw pl [] )
			// we must explicitly stacks caps to ensure they are placed INSIDE the packed value
		>>
	end
	end
	in

	// client code
	open <l,tmp> = newPair({}) in
	open <r,x> = tmp in
		x.initL(12);
		x.initR(30);
		// intentionally switch cap order for testing
		let res = x.sum({}::(rw r int * rw l int)) in
			x.destroy({});
			res
		end
	end
	end

end